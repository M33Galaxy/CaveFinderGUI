plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}
group 'org.example'
version '1.0-SNAPSHOT'
java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
repositories {
    mavenCentral()
    maven { url "https://maven.seedfinding.com" }
    maven { url "https://maven.latticg.com" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.jellejurre.dev" }
}
dependencies {
    implementation('com.seedfinding:mc_core:1.210.0')
    implementation('com.seedfinding:mc_feature:1.171.9')
    implementation('com.seedfinding:mc_seed:1.171.2')
    implementation('com.github.jellejurre:seed-checker:1.2.0-1.18.1') {
        transitive = false
        exclude group: 'org.apache.logging.log4j'
    }
    implementation('com.github.KalleStruik:noise-sampler:1.20.0')
    implementation('com.seedfinding:latticg:1.06@jar')
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
}
jar {
    manifest {
        attributes('Main-Class': 'Launcher')
    }
}

shadowJar {
    archiveBaseName.set('CavefinderGUI')
    archiveClassifier.set('')
    archiveVersion.set('1.0.4')

    manifest {
        attributes 'Main-Class': 'Launcher'
    }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    exclude 'META-INF/LICENSE', 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE', 'META-INF/NOTICE.txt'

    mergeServiceFiles()
}
task CaveFinderJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    group = 'build'
    description = 'Creates a fat JAR for CaveFinder'
    archiveBaseName.set('CaveFinderGUI')
    archiveClassifier.set('')
    archiveVersion.set("1.1.0")
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    manifest {
        attributes 'Main-Class': 'Launcher'
        attributes 'Multi-Release': 'true'
    }
    mergeServiceFiles()
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer) {
        paths = ['META-INF/spring.handlers', 'META-INF/spring.schemas']
        mergeStrategy = "append"
    }
    exclude 'module-info.class'
}
tasks.build.dependsOn tasks.shadowJar